# Extended Backus-Naur Form for Lox.rs:

program         -> declaration* EOF;

declaration     -> varDecl
                 | statement;
varDecl         -> "var" IDENTIFIER ("=" expression)? ";";

statement       -> blockStatement
                 | exprStatement
                 | forStatement
                 | ifStatement
                 | printStatement
                 | whileStatement;
blockStatement  -> "{" statement* "}"
exprStatement   -> expression ";";
forStatement    -> "for" (varDecl | exprStatement | ";") expression? ";" expression? block;
ifStatement     -> "if" expression block ("else" "if" expression block)* ("else" block)?;
printStatement  -> "print" expression ";";
whileStatement  -> "while" expression block;

expression      -> assignment;
assignment      -> IDENTIFIER "=" assignment
                 | logic_or;
logic_or        -> logic_and ("or" logic_and)*;
logic_and       -> equality ("and" equality)*;
equality        -> comparison (("==" | "!=") comparison)*;
comparison      -> term (("<" | "<=" | ">" | ">=") term)*;
term            -> factor (("+" | "-") factor)*;
factor          -> unary (("*" | "/" | "%") unary)*;
unary           -> ("!" | "-") unary | call;
call            -> primary ("(" arguments? ")")*;
primary         -> "(" primary ")" | value | IDENTIFIER;

arguments       -> expression ("," expression)*
value           -> STRING | NUMBER | BOOL | NIL;

IDENTIFIER      -> /[A-Za-z_][A-Za-z0-9_]*/
STRING          -> /"(\\.|[^"])*"/
NUMBER          -> /[0-9]+(\.[0-9]+)?([eE][0-9]+)?/
BOOL            -> /(true)|(false)/
NIL             -> nil
